buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.12.+'
        // compile time only annotation processor and code generator for android studio
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.2'
    }
}
apply plugin: 'android'
apply plugin: 'android-apt'

repositories {
    maven { url 'http://google-api-client-libraries.appspot.com/mavenrepo' }
    // repo for SwipeListView
    maven { url 'http://clinker.47deg.com/nexus/content/groups/public' }
    // repo for DragSortListView
    maven { url 'https://github.com/Goddchen/mvn-repo/raw/master/' }
    mavenCentral()
    mavenLocal()
}

android {
    compileSdkVersion 19
    buildToolsVersion '19.1.0'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    // tells android annotations where the source is
    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java', 'GENERATED_FOLDER']
            resources.srcDirs = ['src/main/resources']
            res.srcDirs = ['src/main/res']
            assets.srcDirs = ['src/main/assets']
        }
    }
}

def AAnnotationsVersion = '3.0.1'

dependencies {
    // Joda Time
    compile 'net.danlew:android.joda:2.3@aar'
    // Google Maps
    compile 'com.google.android.gms:play-services:4.3.23'
    // guava
    compile group: 'com.google.guava', name: 'guava', version: '14.0.+'
    // date pickers
    compile 'com.doomonafireball.betterpickers:library:1.5.2'
    // SwipeListView
    compile('com.fortysevendeg.swipelistview:swipelistview:1.0-SNAPSHOT@aar') {
        transitive = true
    }
    // DragSortListView
    compile 'com.mobeta.android.dslv:drag-sort-listview:0.6.1'

    // SVM library
    compile(group: 'nz.ac.waikato.cms.weka', name: 'LibSVM', version: '1.0.6')

    // Android annotations
    apt "org.androidannotations:androidannotations:$AAnnotationsVersion"
    compile "org.androidannotations:androidannotations-api:$AAnnotationsVersion"

    // App engine - Google API client library.
    compile(group: 'com.google.api-client', name: 'google-api-client', version: '1.17.0-rc') {
        // Exclude artifacts that the Android SDK/Runtime provides.
        exclude(group: 'xpp3', module: 'xpp3')
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
        exclude(group: 'junit', module: 'junit')
        exclude(group: 'com.google.android', module: 'android')
    }

    // App engine - Android extensions for the Google API client library.
    compile(group: 'com.google.api-client', name: 'google-api-client-android', version: '1.17.0-rc') {
        // Exclude play services, since we're not using this yet.
        exclude(group: 'com.google.android.google-play-services', module: 'google-play-services')
        exclude (group: 'org.apache.httpcomponents', module: 'httpclient')
    }

    // App engine - Android extensions for Google HTTP Client.
    compile('com.google.http-client:google-http-client-android:1.17.0-rc') {
        exclude(group: 'com.google.android', module: 'android')
        exclude (group: 'org.apache.httpcomponents', module: 'httpclient')
    }

    // Notifly's server module
    compile('net.notifly.server:messaging:v1-1.18.0-rc-SNAPSHOT') {
        exclude(group: 'org.apache.httpcomponents', module: 'httpclient')
    }

    // Import all libraries under libs folder
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

apt {
    // android-apt arguments for android annotations
    arguments {
        androidManifestFile variant.processResources.manifestFile
        resourcePackageName 'net.notifly.core'
    }
}
